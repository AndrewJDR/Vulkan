macro(ADD_EXAMPLE_BINARY file) 
    if (ANDROID)
        add_library(${TARGET_NAME} SHARED ${EXAMPLE})
        add_dependencies(${TARGET_NAME} app-glue)
        target_link_libraries(${TARGET_NAME} app-glue)
    else()
        add_executable(${TARGET_NAME} ${EXAMPLE})
        set_target_properties(${TARGET_NAME} PROPERTIES FOLDER "examples")
    endif()
endmacro()

macro(BUILD_EXAMPLES)
    foreach(_FOLDER_NAME ${ARGN})
        if ("${_FOLDER_NAME}" STREQUAL "windows")
            if (ANDROID OR (NOT WIN32))
                continue()
            endif()
        endif()
        file(GLOB EXAMPLES ${_FOLDER_NAME}/*.cpp)
        foreach(EXAMPLE ${EXAMPLES})
            get_filename_component(TARGET_NAME ${EXAMPLE} NAME_WE)
            add_example_binary(${EXAMPLE})
            add_dependencies(${TARGET_NAME} base)
            target_include_directories(${TARGET_NAME} PUBLIC base)
            target_link_libraries(${TARGET_NAME} base)
            target_link_libraries(${TARGET_NAME} ${CMAKE_THREAD_LIBS_INIT})
        endforeach()
    endforeach()
endmacro()


MACRO(SUBDIRLIST result curdir)
  FILE(GLOB children RELATIVE ${curdir} ${curdir}/*)
  SET(dirlist "")
  FOREACH(child ${children})
    IF(IS_DIRECTORY ${curdir}/${child})
        LIST(APPEND dirlist ${child})
    ENDIF()
  ENDFOREACH()
  SET(${result} ${dirlist})
ENDMACRO()

subdirlist(SUBDIRS ${CMAKE_CURRENT_SOURCE_DIR})
build_examples(${SUBDIRS})

if ((NOT ANDROID) AND WIN32)

set(TARGET_NAME vr_oculus)
target_oculus()

set(TARGET_NAME vr_openvr)
target_openvr()

endif()